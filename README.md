# AddOn

#This repository is for moving the addon calculations from SAS to R
<<<<<<< HEAD
<<<<<<< HEAD

#SAS Code Below
=======

#SAS Code below
>>>>>>> eed963e984019dd691325c67eef1acbe530f9d3b

/* 06/30/92, ADDON PROGRAM ORIGINALLY WRITTEN                               */
/* 1992-1997, VARIOUS FORMATTING AND DATA MANAGEMENT CHANGES MADE           */
/* 12/02/97, PROGRAM MODIFIED TO PROCESS TAGOTOWEB CSV DOWNLOAD FILES       */
/* 1997-2000, VARIOUS FORMATTING AND DATA MANAGEMENT CHANGES MADE           */
/* 01/28/00, ADDON PROGRAM MODIFIED TO CALCULATE TERMINAL EXCLUSIONS        */
/* 07/02/04, CHANGES TO YAKUTAT TERMINAL EXCLUSION CALCULATIONS             */
/* 12/2011, reactivated TEX file code and commented out CLH catch in CWTMERGE.OUT     */
/* 11/2012, added code to read in proportion large file for gillnet catch*/
/* 7/2013, added Deep Inlet 113-38 as terminal exclusion   */
/* 7/2013, changed code to handle new experimental troll periods and expansions   */
/* 7/2013, changed code to handle new seine area expansions   */
/* 2/2017, changed code to handle new MSF harvest code   */

ods html close; /* close previous results */
ods html; /* open new results */
OPTIONS LS=80 PS=60 MISSING='0' MPRINT MLOGIC;
%GLOBAL CURRYEAR PREVYEAR MAXSTRAT MAXTRAD1 MAXTRAD2;

%Let THISYEAR = 2018;
%Let PATH = S:\PST Chinook\AddOn\2018\Addon18_V7\;
%Let SPORT = SPORT2018_V4;
%Let AKCWTREC = ADDON18G;
%Let NACWTREC = NONAK18G;
%Let EXPFILE = ADDON18G;
%Let CWTREL = ADDON18G;
%Let CHINCAT = ADDON18G;
%Let TROLLPER = ADDON18G;
%Let TERMEX = DIRECT18;
%Let GSIFILE = GSI;
%Let PROPLARGE = 20052018PropLgEstG;

TITLE 'THE ALASKA CHINOOK HATCHERY ADDON';

/* WINDOW WHICH READS THE INPUT FILE NAMES */

/* SPORT CONTRIBUTION FILE MUST HAVE A 'SPT' EXTENSION, (e.g. SP8595.SPT)   */
/* RECOVERY FILES MUST HAVE A 'REC' EXTENSION, (e.g. TROLL95W.REC)          */
/* EXPANSION FILE MUST HAVE A 'EXP' EXTENSION, (e.g. EXP8695.EXP)           */
/* RELEASE FILE MUST HAVE A 'REL' EXTENSION, (e.g. ADDON.REL)               */
/* CATCH FILE MUST HAVE A 'CAT' EXTENSION, (e.g. CAT8495.CAT)               */
/* TROLL PERIODS FILE MUST HAVE A 'PER' EXTENSION, (e.g. TR8495.PER)        */
/* TERMINAL EXCLUSION OPTIONS FILE MUST HAVE A 'TEX' EXTENSION, (e.g. T.TEX)*/
/*
%WINDOW INPUT COLOR=CYAN

  #1 @5 'ENTER THE YEAR OF INTEREST:'
         @50 THISYEAR 4 REQUIRED=NO
  #3 @5 'ENTER THE PATH FOR THE FILES'
         @50 PATH 200 REQUIRED=NO
  #5 @5 'ENTER THE FOLLOWING FILE NAMES (LEAVE OFF EXTENSION FOR EACH FILE)'
  #7 @5 'THE SPORT CONTRIBUTION FILE:'
          @50 SPORT 30 REQUIRED=NO
  #9 @5 'THE ALASKA CWT RECOVERY FILE:'
          @50 AKCWTREC 30 REQUIRED=NO
  #11 @5 'THE NON-ALASKA CWT RECOVERY FILE:'
          @50 NACWTREC 30 REQUIRED=NO
  #13 @5 'THE EXPANSIONS FILE:'
          @50 EXPFILE 30 REQUIRED=NO
  #15 @5 'THE CWT RELEASE FILE:'
          @50 CWTREL 30 REQUIRED=NO
  #17 @5 'THE CHINOOK CATCH FILE:'
          @50 CHINCAT 30 REQUIRED=NO
  #19 @5 'THE TROLL AND SEINE PERIODS FILE:'
          @50 TROLLPER 30 REQUIRED=NO
  #21 @5 'THE TERMINAL EXCLUSION OPTION FILE:'
          @50 TERMEX 30 REQUIRED=NO
  #23 @5 'THE TERMINAL EXCLUSION GSI FILE:'
          @50 GSIFILE 30 REQUIRED=NO
  #25 @5 'THE Proportion Large FILE:'
          @50 PROPLARGE 30 REQUIRED=NO;

%DISPLAY INPUT;
*/
/* CODE WHICH IDENTIFIES THE CURRENT AND THE PREVIOUS YEAR AND */
/* THE LAST TROLL PERIOD FOR THE CURRENT AND THE PREVIOUS YEAR */

%MACRO PICKYEAR;

   %LET CURRYEAR=%EVAL(&THISYEAR);
   %LET PREVYEAR=%EVAL(&CURRYEAR-1);

%MEND;

%PICKYEAR

/* CREATING GEAR, HARVEST AND QUADRANT FORMATS */

PROC FORMAT;
   VALUE $GEARFMT '0' ='TRAP'
                  '00'='TRAP'
                  '90'='TRAP'
                  '1' ='SEINE'
                  '01'='SEINE'
                  '2' ='BEACH SEINE'
                  '02'='BEACH SEINE'
                  '3' ='GILLNET'
                  '03'='GILLNET'
                  '4' ='SETNET'
                  '04'='SETNET'
                  '5' ='TROLL'
                  '05'='TROLL'
                  '15'='TROLL'
                  '99'='UNKNOWN';
RUN;

PROC FORMAT;
   VALUE $HARVFMT  '10'='MARK SELECT FISHY'
                   '11'='TRADITIONAL'
                   '12'='TERMINAL AREA'
                   '13'='EXPERIM. AREA'
                   '14'='EXPERIM. GEAR'
                   '17'='M-I-C'
                   '18'='CONFISCATED'
                   '1M'='MULTIPLE'
                   '91'='STIKINE EXCLUSION'
                   '92'='TAKU EXCLUSION'
                   '93'='YAKUTAT EXCLUSION';
RUN;

PROC FORMAT;
   VALUE $QUADFMT   '101'='SE'
                    '102'='SE'
                    '105'='SE'
                    '106'='SE'
                    '107'='SE'
                    '108'='SE'
                    '109'='NE'
                    '110'='NE'
                    '111'='NE'
                    '112'='NE'
                    '115'='NE'
                    '103'='SW'
                    '104'='SW'
                    '150'='SW'
                    '152'='SW'
                    '113'='NW'
                    '114'='NW'
                    '116'='NW'
                    '154'='NW'
                    '156'='NW'
                    '157'='NW'
                    '181'='NW'
                    '182'='NW'
                    '183'='NW'
                    '184'='NW'
                    '185'='NW'
                    '186'='NW'
                    '189'='NW'
                    '191'='NW'
                    '192'='NW';
RUN;

PROC FORMAT;
   VALUE $SEINEAREAFMT  '101'='SASI'
                        '102'='SASI'
                        '103'='SASO'
                        '104'='SASO'
                        '105'='SACI'
                        '106'='SACI'
                        '107'='SACI'
                        '109'='SACO'
                        '110'='SACO'
                        '111'='SA11'
                        '112'='SA12'
                        '113'='SA13'
                        '114'='SA14';
RUN;

/* DATA STEPS WHICH READ AND IDENTIFY THE TROLL PERIODS */

DATA PERIODIN;
   INFILE "&PATH.&TROLLPER..PER" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT YEAR GEARNUM $ WEEK PERIOD HARNUM;
   IF GEARNUM='5' AND HARNUM NE 13;
   GEAR=PUT(GEARNUM,$GEARFMT.);
   DROP HARNUM GEARNUM;
RUN;

DATA PERIODIN2;
   INFILE "&PATH.&TROLLPER..PER" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT YEAR GEARNUM $ WEEK PERIOD HARNUM;
   IF GEARNUM='5' AND HARNUM = 13;
   GEAR=PUT(GEARNUM,$GEARFMT.);
   DROP HARNUM GEARNUM;
RUN;

DATA PERIODIN3;
   INFILE "&PATH.&TROLLPER..PER" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT YEAR GEARNUM $ WEEK PERIOD HARNUM;
   IF GEARNUM='1';
   GEAR=PUT(GEARNUM,$GEARFMT.);
   DROP HARNUM GEARNUM;
RUN;

PROC SORT DATA=PERIODIN;
   BY YEAR GEAR WEEK;
RUN;

PROC SORT DATA=PERIODIN2;
   BY YEAR GEAR WEEK;
RUN;

PROC SORT DATA=PERIODIN3;
   BY YEAR GEAR WEEK;
RUN;

PROC MEANS DATA=PERIODIN NOPRINT;
   VAR PERIOD;
   BY YEAR;
   WHERE YEAR IN (&PREVYEAR,&CURRYEAR);
   OUTPUT OUT=BOUNDS MAX=LAST;
RUN;

PROC TRANSPOSE DATA=BOUNDS OUT=LASTOUT;
   VAR LAST;
RUN;

DATA _NULL_;
   SET LASTOUT;
   CALL SYMPUT('MAXTRAD1',COL1);
   CALL SYMPUT('MAXTRAD2',COL2);
RUN;

/* DATA STEPS WHICH READ AND SORT THE RECOVERY FILES */

DATA READREC;
   LENGTH DISTRICT $ 3 SUBDIST $ 2 DISTSUB $ 6 HARVEST $ 17 CODE $ 10;
   INFORMAT DATE YYMMDD10.;
   INFILE "&PATH.&AKCWTREC..REC" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT YEAR HEADNUM $ SAMPLE $ CODE $ SPECCODE HATCHERY $ HARNUM $ GEARNUM $
         DATE WEEK PERIOD DISTRICT $ SUBDIST $ QUADRANT $ SEINEAREA $ LENCODE LENGTH;
   GEAR=PUT(GEARNUM,$GEARFMT.);
   HARVEST=PUT(HARNUM,$HARVFMT.);
      IF YEAR IN (&PREVYEAR,&CURRYEAR);
   NDIST=DISTRICT*1;
   IF (GEAR='GILLNET' AND (116 < NDIST < 200)) THEN GEAR='SETNET';
   MONTHNUM=MONTH(DATE);
   DAY=DAY(DATE);
   DISTSUB=TRIM(DISTRICT)||'-'||SUBDIST;
   IF (YEAR=1997 AND GEAR="TROLL" AND MONTHNUM=8 AND DAY=30) THEN DO;
      DAY=31;
      WEEK=WEEK+1;
      END;
   DROP SAMPLE LENGTH LENCODE GEARNUM HARNUM;
RUN;

DATA RECOVERY;
   SET READREC;
   IF (YEAR=&CURRYEAR) OR (GEAR="TROLL" AND YEAR=&PREVYEAR AND
       PERIOD=&MAXTRAD1);
   IF (YEAR=1989) AND (MONTHNUM=6) AND (DAY > 23) AND
      (HARVEST IN ("TRADITIONAL","MULTIPLE","CONFISCATED")) AND
      (GEAR="TROLL") THEN PERIOD=PERIOD-1;
RUN;

PROC SORT DATA=RECOVERY;
   BY CODE;
RUN;

/* DATA STEPS WHICH CALCULATE THE TERMINAL EXCLUSION FLAGS */

DATA READTEX;
   INFILE "&PATH.&TERMEX..TEX" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT YEAR RIVER $ DIRECT $;
   IF YEAR=&CURRYEAR;
   IF DIRECT = "Y" THEN EXFLAG=1;
   ELSE EXFLAG=0;
RUN;

PROC SORT DATA=READTEX;
   BY YEAR RIVER;
RUN;

PROC TRANSPOSE DATA=READTEX OUT=TRANTEX;
   VAR EXFLAG;
RUN;

DATA _NULL_;
   SET TRANTEX;
   CALL SYMPUT('STIKFLAG',COL1);
   CALL SYMPUT('TAKUFLAG',COL2);
RUN;

/* DETERMINING STRATA (ID) FOR THE TIME-AREA EXPANSIONS */

DATA FISHERY;
   SET RECOVERY;
   IF GEAR NE 'TROLL' THEN ID=1;
   ELSE IF GEAR='TROLL' THEN DO;
      IF (1989 LE YEAR LE 1992) AND (5 LE MONTHNUM LE 6) AND
         (HARVEST NE "M-I-C") THEN ID=3;
      ELSE ID=2;
      END;
   IF (HARVEST EQ "EXPERIM. AREA") THEN DO;
      IF (YEAR GE 1996 AND DISTSUB="112-22") THEN DO;
         ID=5;
         HARVEST="TERMINAL AREA";
         END;
      ELSE IF (YEAR GE 2005) THEN DO;
         IF (DISTRICT="108" AND SUBDIST NE "45" AND &STIKFLAG=1) THEN ID=9;
         ELSE IF (DISTRICT="111" AND &TAKUFLAG=1)THEN ID=10;
         ELSE ID=4;
         END;
      ELSE ID=4;
      END;
   IF (HARVEST EQ "TERMINAL AREA") THEN ID=5;
   IF (GEAR = "GILLNET" and DISTRICT="111" AND &TAKUFLAG=1) THEN ID=6;
   IF ( GEAR = "GILLNET" and DISTRICT="108" AND SUBDIST NE "45" AND &STIKFLAG=1) THEN ID=7;
   IF (GEAR="SETNET" AND ((1996 LE YEAR LE 2002) OR (YEAR GT 2002 AND DISTSUB="182-70"))) THEN ID=8;
   IF GEAR = "SEINE" AND HARVEST = "TRADITIONAL" THEN ID = 11;
RUN;

/* DATA STEP WHICH READS THE CWT RELEASE FILE */

DATA RELEASE;
   LENGTH CODE $ 10;
   INFILE "&PATH.&CWTREL..REL" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT CODE $ SPECNUM BROOD RELYEAR TAGGED RELEASED;
   IF INDEX(CODE,'*')=0;
RUN;

PROC SORT DATA=RELEASE;
   BY CODE;
RUN;

/* MERGEING THE RECOVERY AND RELEASE FILES */

DATA MERGEM1;
   MERGE FISHERY RELEASE;
   BY CODE;
   COUNT=1;
RUN;

/* SPLITTING THE COMBINED RECOVERY-RELEASE FILE BASED ON EXPANSION TYPE */

DATA NETID TRADID HATID EXPID TERMID NTAKUID NSTIKID YAKID TSTIKID TTAKUID TRADSEINEID;
   SET MERGEM1;
   IF (RELEASED NE .) AND (HATCHERY NE " ") AND (NDIST < 200);
   IF ID=1 THEN OUTPUT NETID;
   ELSE IF ID=2 THEN OUTPUT TRADID;
   ELSE IF ID=3 THEN OUTPUT HATID;
   ELSE IF ID=4 THEN OUTPUT EXPID;
   ELSE IF ID=5 THEN OUTPUT TERMID;
   ELSE IF ID=6 THEN OUTPUT NTAKUID;
   ELSE IF ID=7 THEN OUTPUT NSTIKID;
   ELSE IF ID=8 THEN OUTPUT YAKID;
   ELSE IF ID=9 THEN OUTPUT TSTIKID;
   ELSE IF ID=10 THEN OUTPUT TTAKUID;
   ELSE IF ID=11 THEN OUTPUT TRADSEINEID;
RUN;

PROC MEANS DATA=HATID NOPRINT;
   VAR PERIOD;
   OUTPUT OUT=HATBOUND MIN=HATMIN MAX=HATMAX;
RUN;

%MACRO HATFLAG;

   %GLOBAL MINPER;
   %GLOBAL MAXPER;
   %LET MINPER=0;
   %LET MAXPER=0;

   DATA _NULL_;
      SET HATBOUND;
      CALL SYMPUT('MINPER',HATMIN);
      CALL SYMPUT('MAXPER',HATMAX);
   RUN;

%MEND;

%HATFLAG

/* SORTING THE COMBINED RECOVERY-RELEASE FILES INTO STRATA*/

PROC SORT DATA=NETID;
   BY YEAR GEAR HARVEST WEEK DISTRICT CODE;
RUN;

PROC SORT DATA=TRADID;
   BY YEAR GEAR HARVEST PERIOD QUADRANT CODE;
RUN;

PROC SORT DATA=HATID;
   BY YEAR GEAR HARVEST PERIOD QUADRANT CODE;
RUN;

PROC SORT DATA=EXPID;
   BY YEAR GEAR HARVEST PERIOD DISTRICT CODE;
RUN;

PROC SORT DATA=TERMID;
   BY YEAR GEAR HARVEST WEEK DISTSUB CODE;
RUN;

PROC SORT DATA=NTAKUID;
   BY YEAR GEAR HARVEST WEEK DISTRICT CODE;
RUN;

PROC SORT DATA=NSTIKID;
   BY YEAR GEAR HARVEST WEEK DISTRICT CODE;
RUN;

PROC SORT DATA=YAKID;
   BY YEAR GEAR HARVEST WEEK DISTSUB CODE;
RUN;

PROC SORT DATA=TTAKUID;
   BY YEAR GEAR HARVEST PERIOD DISTRICT CODE;
RUN;

PROC SORT DATA=TSTIKID;
   BY YEAR GEAR HARVEST PERIOD DISTRICT CODE;
RUN;

PROC SORT DATA=TRADSEINEID;
   BY YEAR GEAR HARVEST PERIOD SEINEAREA CODE;
RUN;

/* COMPUTING THE FREQUENCY OF EACH CODE IN EACH STRATA */

PROC MEANS DATA=NETID NOPRINT;
   VAR COUNT;
   BY YEAR GEAR HARVEST WEEK DISTRICT CODE;
   ID HATCHERY TAGGED RELEASED BROOD ID;
   OUTPUT OUT=NETOUT SUM=COUNT;
RUN;

PROC MEANS DATA=TRADID NOPRINT;
   VAR COUNT;
   BY YEAR GEAR HARVEST PERIOD QUADRANT CODE;
   ID HATCHERY TAGGED RELEASED BROOD ID;
   OUTPUT OUT=TRADOUT SUM=COUNT;
RUN;

PROC MEANS DATA=HATID NOPRINT;
   VAR COUNT;
   BY YEAR GEAR HARVEST PERIOD QUADRANT CODE;
   ID HATCHERY TAGGED RELEASED BROOD ID;
   OUTPUT OUT=HATOUT SUM=COUNT;
RUN;

PROC MEANS DATA=EXPID NOPRINT;
   VAR COUNT;
   BY YEAR GEAR HARVEST PERIOD DISTRICT CODE;
   ID HATCHERY TAGGED RELEASED BROOD ID;
   OUTPUT OUT=EXPOUT SUM=COUNT;
RUN;

PROC MEANS DATA=TERMID NOPRINT;
   VAR COUNT;
   BY YEAR GEAR HARVEST WEEK DISTSUB CODE;
   ID HATCHERY TAGGED RELEASED BROOD ID;
   OUTPUT OUT=TERMOUT SUM=COUNT;
RUN;

PROC MEANS DATA=NTAKUID NOPRINT;
   VAR COUNT;
   BY YEAR GEAR HARVEST WEEK DISTRICT CODE;
   ID HATCHERY TAGGED RELEASED BROOD ID;
   OUTPUT OUT=TAKUOUT SUM=COUNT;
RUN;

PROC MEANS DATA=NSTIKID NOPRINT;
   VAR COUNT;
   BY YEAR GEAR HARVEST WEEK DISTRICT CODE;
   ID HATCHERY TAGGED RELEASED BROOD ID;
   OUTPUT OUT=STIKOUT SUM=COUNT;
RUN;

PROC MEANS DATA=TTAKUID NOPRINT;
   VAR COUNT;
   BY YEAR GEAR HARVEST PERIOD DISTRICT CODE;
   ID HATCHERY TAGGED RELEASED BROOD ID;
   OUTPUT OUT=TTAKUOUT SUM=COUNT;
RUN;

PROC MEANS DATA=TSTIKID NOPRINT;
   VAR COUNT;
   BY YEAR GEAR HARVEST PERIOD DISTRICT CODE;
   ID HATCHERY TAGGED RELEASED BROOD ID;
   OUTPUT OUT=TSTIKOUT SUM=COUNT;
RUN;

PROC MEANS DATA=TRADSEINEID NOPRINT;
   VAR COUNT;
   BY YEAR GEAR HARVEST PERIOD SEINEAREA CODE;
   ID HATCHERY TAGGED RELEASED BROOD ID;
   OUTPUT OUT=TRADSEINEOUT SUM=COUNT;
RUN;

%MACRO YAKMEAN;

%IF (1996 LE &CURRYEAR) AND (&CURRYEAR LE 2002) %THEN %DO;

   PROC MEANS DATA=YAKID NOPRINT;
      VAR COUNT;
      BY YEAR GEAR HARVEST WEEK DISTRICT CODE;
      ID HATCHERY TAGGED RELEASED BROOD ID;
      OUTPUT OUT=YAKOUT SUM=COUNT;
   RUN;

   %END;

%ELSE %IF (&CURRYEAR GE 2003) %THEN %DO;

   PROC MEANS DATA=YAKID NOPRINT;
      VAR COUNT;
      BY YEAR GEAR HARVEST WEEK DISTSUB CODE;
      ID HATCHERY TAGGED RELEASED BROOD ID;
      OUTPUT OUT=YAKOUT SUM=COUNT;
   RUN;

   %END;

%MEND;

%YAKMEAN

/* DATA STEP WHICH READS IN THE Proportion Large FILE */

DATA LARGEIN;
   LENGTH DISTRICT $ 3;
   INFILE "&PATH.&PROPLARGE..LRG" FIRSTOBS=2 DSD MISSOVER PAD;
   INPUT YEAR GEARNUM $ DISTRICT $ WEEK PROPLG DATATYPE $;
   GEAR=PUT(GEARNUM,$GEARFMT.);
   DROP DATATYPE;
RUN;

PROC SORT DATA=LARGEIN;
   BY YEAR GEAR DISTRICT WEEK;
RUN;

/* DATA STEP WHICH READS IN THE CATCH FILE */

DATA CATIN;
   LENGTH DISTRICT $ 3 SUBDIST $ 2 DISTSUB $ 6;
   INFILE "&PATH.&CHINCAT..CAT" FIRSTOBS=2 DSD MISSOVER PAD;
   INPUT YEAR HARNUM $ GEARNUM $ DISTRICT $ SUBDIST $ WEEK N SUBN;
   IF (YEAR GE 1996) AND (HARNUM="13") AND (DISTRICT="112") AND (SUBDIST="22")
      THEN HARNUM="12";
   IF GEARNUM="90" THEN GEARNUM="00";
   GEAR=PUT(GEARNUM,$GEARFMT.);
   HARVEST=PUT(HARNUM,$HARVFMT.);
   DISTSUB=TRIM(DISTRICT)||'-'||SUBDIST;
   QUADRANT=PUT(DISTRICT,$QUADFMT.);
   SEINEAREA = PUT(DISTRICT,$SEINEAREAFMT.);
   IF (YEAR=2000) AND (GEAR="TROLL") AND (HARVEST="TRADITIONAL") AND (N > 0) THEN DO;
      IF WEEK IN (29,30,31,32) THEN WEEK=33;
      ELSE IF WEEK=37 THEN WEEK=38;
      END;
   DROP GEARNUM;
RUN;

PROC SORT DATA=CATIN;
   BY YEAR GEAR DISTRICT WEEK;
RUN;

/*non-experimental Troll catch*/
Data TTROLLCATCH;
   SET CATIN;
   IF GEAR = "TROLL" and HARNUM NE '13';
   DROP HARNUM;
RUN;

PROC SORT DATA=TTROLLCATCH;
   BY YEAR GEAR WEEK;
RUN;

/*experimental Troll catch*/
Data EXTROLLCATCH;
   SET CATIN;
   IF GEAR = "TROLL" and HARNUM = '13';
   DROP HARNUM;
RUN;

PROC SORT DATA=EXTROLLCATCH;
   BY YEAR GEAR WEEK;
RUN;

Data TTROLLMERGE;
   MERGE TTROLLCATCH PERIODIN;
   BY YEAR GEAR WEEK;
RUN;

PROC SORT DATA=TTROLLMERGE;
   BY YEAR GEAR WEEK;
RUN;

Data EXTROLLMERGE;
   MERGE EXTROLLCATCH PERIODIN2;
   BY YEAR GEAR WEEK;
RUN;

PROC SORT DATA=EXTROLLMERGE;
   BY YEAR GEAR WEEK;
RUN;

/*Non-Troll catch*/
DATA CATINLARGE;
   MERGE CATIN LARGEIN;
   BY YEAR GEAR DISTRICT WEEK;
   IF GEAR NE "TROLL";
   IF (YEAR GE 2005) and ((GEAR = "GILLNET") or (GEAR = "SETNET" and DISTRICT IN (182,183)))
   THEN N = N*PROPLG;
   DROP HARNUM PROPLG;
RUN;

PROC SORT DATA=CATINLARGE;
   BY YEAR GEAR WEEK;
RUN;

DATA MERGWEEK;
   SET CATINLARGE TTROLLMERGE EXTROLLMERGE;
   /*MERGE CATINLARGE TTROLLMERGE EXTROLLMERGE;
   /*BY YEAR GEAR WEEK;*/
RUN;

DATA TRIMWEEK;
   SET MERGWEEK;
   IF (N NE 0) AND (HARVEST NE "");
RUN;

/* DATA STEP WHICH READS THE NET EXPANSIONS Except Traditional Seine */
DATA NETEXPAN;
   LENGTH DISTRICT $ 3 GEAR $ 11;
   INFILE "&PATH.&EXPFILE..EXP" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT YEAR HARNUM $ SPECNUM GEARNUM $ TIMECODE $ WEEK AREACODE $
         DISTRICT $ OLDN N2 A1 A2 M1 M2;
   IF (GEARNUM NE '5') AND (HARNUM NE '12');
   IF GEARNUM='1' THEN NDIST=0;
   ELSE NDIST=DISTRICT*1;
   IF (GEARNUM='3' AND (116 < NDIST < 200)) THEN GEAR='SETNET';
   ELSE GEAR=PUT(GEARNUM,$GEARFMT.);
   HARVEST=PUT(HARNUM,$HARVFMT.);
   KEEP YEAR HARVEST GEAR WEEK DISTRICT NDIST OLDN N2 A1 A2 M1 M2;
RUN;

DATA NETEXPAN2;
   SET NETEXPAN;
   IF GEAR NE 'SEINE' AND NDIST LT 200;
RUN;

PROC SORT DATA=NETEXPAN2;
   BY YEAR GEAR HARVEST WEEK DISTRICT;
RUN;

PROC SORT DATA=TRIMWEEK;
   BY YEAR GEAR HARVEST WEEK DISTRICT;
RUN;

PROC MEANS DATA=TRIMWEEK NOPRINT;
   VAR N;
   BY YEAR GEAR HARVEST WEEK DISTRICT;
   OUTPUT OUT=NETWEEK SUM=N;
RUN;

DATA MERGENET;
   MERGE NETEXPAN2 NETWEEK;
   BY YEAR GEAR HARVEST WEEK DISTRICT;
   IF GEAR NE "TROLL" AND HARVEST NE "TERMINAL AREA";
   IF GEAR = "SEINE" and HARVEST = "TRADITIONAL" then DELETE;
RUN;

/* DATA STEP WHICH READS THE TRADITIONAL AND */
/* HATCHERY ACCESS TROLL EXPANSIONS */

DATA TRADXPAN;
   INFILE "&PATH.&EXPFILE..EXP" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT YEAR HARNUM $ SPECNUM GEARNUM $ TIMECODE $ PERIOD AREACODE $
         QUADRANT $ OLDN N2 A1 A2 M1 M2;
   IF (GEARNUM='5') AND (HARNUM IN ('10','11','17','18'));
   GEAR=PUT(GEARNUM,$GEARFMT.);
   HARVEST=PUT(HARNUM,$HARVFMT.);
   KEEP YEAR HARVEST GEAR PERIOD QUADRANT OLDN N2 A1 A2 M1 M2;
RUN;

PROC SORT DATA=TRADXPAN;
   BY YEAR GEAR HARVEST PERIOD QUADRANT;
RUN;

PROC SORT DATA=TRIMWEEK;
   BY YEAR GEAR HARVEST PERIOD QUADRANT;
RUN;

PROC MEANS DATA=TRIMWEEK NOPRINT;
   VAR N;
   BY YEAR GEAR HARVEST PERIOD QUADRANT;
   OUTPUT OUT=TRADWEEK SUM=N;
RUN;

DATA MERGTRAD;
   MERGE TRADXPAN TRADWEEK;
   BY YEAR GEAR HARVEST PERIOD QUADRANT;
   IF GEAR="TROLL" AND HARVEST NE "TERMINAL AREA" AND
      HARVEST NE "EXPERIM. AREA";
RUN;

/* DATA STEP WHICH READS THE EXPERIMENTAL TROLL EXPANSIONS */

DATA EXPEXPAN;
   LENGTH DISTRICT $ 3;
   INFILE "&PATH.&EXPFILE..EXP" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT YEAR HARNUM $ SPECNUM GEARNUM $ TIMECODE $ PERIOD AREACODE $
         DISTRICT $ OLDN N2 A1 A2 M1 M2;
   IF (GEARNUM='5') AND (HARNUM='13');
   NDIST=DISTRICT*1;
   GEAR=PUT(GEARNUM,$GEARFMT.);
   HARVEST=PUT(HARNUM,$HARVFMT.);
   KEEP YEAR HARVEST GEAR PERIOD DISTRICT NDIST OLDN N2 A1 A2 M1 M2;
RUN;

PROC SORT DATA=EXPEXPAN;
   BY YEAR GEAR HARVEST PERIOD DISTRICT;
RUN;

PROC SORT DATA=TRIMWEEK;
   BY YEAR GEAR HARVEST PERIOD DISTRICT;
RUN;

PROC MEANS DATA=TRIMWEEK NOPRINT;
   VAR N;
   BY YEAR GEAR HARVEST PERIOD DISTRICT;
   OUTPUT OUT=EXPWEEK SUM=N;
RUN;

DATA MERGEEXP;
   MERGE EXPEXPAN EXPWEEK;
   BY YEAR GEAR HARVEST PERIOD DISTRICT;
   IF GEAR="TROLL" AND HARVEST="EXPERIM. AREA";
RUN;

/* DATA STEP WHICH READS THE TERMINAL EXPANSIONS */

DATA TRMEXPAN;
   LENGTH DISTSUB $ 6;
   INFILE "&PATH.&EXPFILE..EXP" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT YEAR HARNUM $ SPECNUM GEARNUM $ TIMECODE $ WEEK AREACODE $
         TDISTSUB $ OLDN N2 A1 A2 M1 M2;
   IF HARNUM='12';
   DISTSUB=SUBSTR(TDISTSUB,1,3)||'-'||SUBSTR(TDISTSUB,4,2);
   GEAR=PUT(GEARNUM,$GEARFMT.);
   HARVEST=PUT(HARNUM,$HARVFMT.);
   KEEP YEAR HARVEST GEAR WEEK DISTSUB OLDN N2 A1 A2 M1 M2;
RUN;

PROC SORT DATA=TRMEXPAN;
   BY YEAR GEAR HARVEST WEEK DISTSUB;
RUN;

PROC SORT DATA=TRIMWEEK;
   BY YEAR GEAR HARVEST WEEK DISTSUB;
RUN;

PROC MEANS DATA=TRIMWEEK NOPRINT;
   VAR N;
   BY YEAR GEAR HARVEST WEEK DISTSUB;
   OUTPUT OUT=TERMWEEK SUM=N;
RUN;

DATA MERGETRM;
   MERGE TRMEXPAN TERMWEEK;
   BY YEAR GEAR HARVEST WEEK DISTSUB;
   IF HARVEST="TERMINAL AREA";
RUN;

/* DATA STEP WHICH READS THE Traditional Seine EXPANSIONS */
DATA TRADSEINXPAN;
   INFILE "&PATH.&EXPFILE..EXP" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT YEAR HARNUM $ SPECNUM GEARNUM $ TIMECODE $ PERIOD AREACODE $
         SEINEAREA $ OLDN N2 A1 A2 M1 M2;
   IF (GEARNUM='1') AND (HARNUM = '11');
   GEAR=PUT(GEARNUM,$GEARFMT.);
   HARVEST=PUT(HARNUM,$HARVFMT.);
   KEEP YEAR HARVEST GEAR PERIOD SEINEAREA OLDN N2 A1 A2 M1 M2;
RUN;

PROC SORT DATA=TRADSEINXPAN;
   BY YEAR GEAR HARVEST PERIOD SEINEAREA;
RUN;

DATA SEINEWEEK;
   SET TRIMWEEK;
   IF GEAR='SEINE' AND HARVEST='TRADITIONAL';
   DROP PERIOD;
RUN;

PROC SORT DATA=SEINEWEEK;
   BY YEAR GEAR WEEK;
RUN;

DATA MERGESEINEWEEK;
   MERGE SEINEWEEK PERIODIN3;
   BY YEAR GEAR WEEK;
RUN;

PROC SORT DATA=MERGESEINEWEEK;
   BY YEAR GEAR HARVEST PERIOD SEINEAREA;
RUN;

PROC MEANS DATA=MERGESEINEWEEK NOPRINT;
   VAR N;
   BY YEAR GEAR HARVEST PERIOD SEINEAREA;
   OUTPUT OUT=TRADSEINEWEEK SUM=N;
RUN;

DATA MERGTRADSEINE;
   MERGE TRADSEINXPAN TRADSEINEWEEK;
   BY YEAR GEAR HARVEST PERIOD SEINEAREA;
   IF GEAR="SEINE" AND HARVEST NE "TERMINAL AREA";
RUN;


/* MERGING THE EXPANSION FILES WITH THE COMBINED RECOVERY-RELEASE FILES */

DATA MERGEM2A;
   MERGE NETOUT MERGENET;
   BY YEAR GEAR HARVEST WEEK DISTRICT;
RUN;

DATA MERGEM2B;
   LENGTH HARVEST $ 17;
   MERGE TAKUOUT MERGENET;
   BY YEAR GEAR HARVEST WEEK DISTRICT;
   IF (GEAR="GILLNET" AND DISTRICT="111"
       AND &TAKUFLAG=1);
   RENAME N=TAKUN;
RUN;

DATA FIXEM2B;
   SET MERGEM2B;
   IF HARVEST NE "CONFISCATED";
   HARVEST="TAKU EXCLUSION";
   RENAME TAKUN=N;
RUN;

DATA MERGEM2C;
   LENGTH HARVEST $ 17;
   MERGE STIKOUT MERGENET;
   BY YEAR GEAR HARVEST WEEK DISTRICT;
   IF ( GEAR = "GILLNET" and DISTRICT="108" AND SUBDIST NE "45" AND &STIKFLAG=1);
   RENAME N=STIKN;
RUN;

DATA FIXEM2C;
   SET MERGEM2C;
   IF HARVEST NE "CONFISCATED";
   HARVEST="STIKINE EXCLUSION";
   RENAME STIKN=N;
RUN;

PROC SORT DATA=FIXEM2C;
   BY YEAR GEAR HARVEST WEEK DISTRICT;
RUN;

%MACRO MERGEYAK;

%IF (1996 LE &CURRYEAR) AND (&CURRYEAR LE 2002) %THEN %DO;

   DATA MERGEM2D;
      LENGTH HARVEST $ 17;
      MERGE YAKOUT MERGENET;
      BY YEAR GEAR HARVEST WEEK DISTRICT;
      IF GEAR="SETNET";
      RENAME N=YAKN;
   RUN;

   %END;

%ELSE %IF (&CURRYEAR GE 2003) %THEN %DO;

   DATA MERGEM2D;
      LENGTH DISTRICT $ 3 HARVEST $ 17;
      MERGE YAKOUT TERMWEEK;
      BY YEAR GEAR HARVEST WEEK DISTSUB;
      IF (GEAR="SETNET" AND DISTSUB="182-70");
      DISTRICT=SUBSTR(DISTSUB,1,3);
      RENAME N=YAKN;
   RUN;

   %END;

%MEND;

%MERGEYAK

DATA FIXEM2D;
   SET MERGEM2D;
   HARVEST="YAKUTAT EXCLUSION";
   RENAME YAKN=N;
RUN;

PROC SORT DATA=FIXEM2D;
   BY YEAR GEAR HARVEST WEEK DISTRICT;
RUN;

DATA MERGEM2;
   LENGTH HARVEST $ 17;
   MERGE MERGEM2A MERGEM2B FIXEM2B MERGEM2C FIXEM2C MERGEM2D FIXEM2D;
   BY YEAR GEAR HARVEST WEEK DISTRICT;
RUN;

DATA MERGEM3;
   MERGE TRADOUT MERGTRAD;
   BY YEAR GEAR HARVEST PERIOD QUADRANT;
RUN;

DATA MERGEM4;
   MERGE HATOUT MERGTRAD;
   BY YEAR GEAR HARVEST PERIOD QUADRANT;
   IF HARVEST="TRADITIONAL" THEN HARVEST="HATCHERY ACCESS";
RUN;

DATA MERGEM5A;
   MERGE EXPOUT MERGEEXP;
   BY YEAR GEAR HARVEST PERIOD DISTRICT;
RUN;

DATA MERGEM5B;
   LENGTH HARVEST $ 17;
   MERGE TTAKUOUT MERGEEXP;
   BY YEAR GEAR HARVEST PERIOD DISTRICT;
   IF (GEAR="TROLL" AND DISTRICT="111"
       AND &TAKUFLAG=1);
   RENAME N=TAKUN;
RUN;

DATA FIXEM5B;
   SET MERGEM5B;
   HARVEST="TAKU EXCLUSION";
   RENAME TAKUN=N;
RUN;

DATA MERGEM5C;
   LENGTH HARVEST $ 17;
   MERGE TSTIKOUT MERGEEXP;
   BY YEAR GEAR HARVEST PERIOD DISTRICT;
   IF ( GEAR = "TROLL" and DISTRICT="108" AND SUBDIST NE "45" AND &STIKFLAG=1);
   RENAME N=STIKN;
RUN;

DATA FIXEM5C;
   SET MERGEM5C;
   HARVEST="STIKINE EXCLUSION";
   RENAME STIKN=N;
RUN;

DATA MERGEM5;
   MERGE MERGEM5A MERGEM5B FIXEM5B MERGEM5C FIXEM5C;
   BY YEAR GEAR HARVEST PERIOD DISTRICT;
RUN;

DATA MERGEM6;
   MERGE TERMOUT MERGETRM;
   BY YEAR GEAR HARVEST WEEK DISTSUB;
RUN;

DATA MERGEM7;
   MERGE TRADSEINEOUT MERGTRADSEINE;
   BY YEAR GEAR HARVEST PERIOD SEINEAREA;
RUN;


/* THE FINAL MERGEING OF ALL THE RECOVERY-RELEASE-EXPANSION FILES */

DATA TRIMNET;
   SET MERGEM2;
   NDIST=DISTRICT*1;
   IF ((NDIST LT 200) AND (NDIST NE .)) OR HARVEST IN ("STIKINE EXCLUSION",
      "TAKU EXCLUSION","YAKUTAT EXCLUSION");
   IF YEAR=&CURRYEAR;
   DUMMY=1;
RUN;

DATA TRIMTRAD;
   SET MERGEM3;
   IF QUADRANT NE " ";
   IF (YEAR=&PREVYEAR AND PERIOD=&MAXTRAD1) OR
      (YEAR=&CURRYEAR AND ((HARVEST EQ "M-I-C") OR (PERIOD LT &MINPER) OR
      (&MAXPER LT PERIOD LT &MAXTRAD2)));
   DUMMY=2;
RUN;

DATA TRIMHAT;
   SET MERGEM4;
   IF QUADRANT NE " ";
   IF (&MINPER <= PERIOD <= &MAXPER) AND (HARVEST EQ "HATCHERY ACCESS");
   IF YEAR=&CURRYEAR;
   DUMMY=3;
RUN;

DATA TRIMEXP;
   SET MERGEM5;
   IF (NDIST LT 200) AND (NDIST NE .);
   IF YEAR=&CURRYEAR;
   DUMMY=4;
RUN;

DATA TRIMTERM;
   SET MERGEM6;
   IF DISTSUB NE " ";
   IF YEAR=&CURRYEAR;
   DUMMY=5;
RUN;

DATA TRIMSEINE;
   SET MERGEM7;
   IF YEAR=&CURRYEAR;
   DUMMY=6;
RUN;

DATA MERGEM8;
   LENGTH AREA $ 6;
   MERGE TRIMNET TRIMTRAD TRIMHAT TRIMEXP TRIMTERM TRIMSEINE;
   BY DUMMY;
   IF DUMMY=1 THEN DO;
      TIME=WEEK;
      IF (HARVEST="YAKUTAT EXCLUSION" AND YEAR GE 2003) THEN AREA=DISTSUB;
      ELSE AREA=DISTRICT;
      END;
   ELSE IF DUMMY=2 THEN DO;
      TIME=PERIOD;
      AREA=QUADRANT;
      END;
   ELSE IF DUMMY=3 THEN DO;
      TIME=PERIOD;
      AREA=QUADRANT;
      END;
   ELSE IF DUMMY=4 THEN DO;
      TIME=PERIOD;
      AREA=DISTRICT;
      END;
   ELSE IF DUMMY=5 THEN DO;
      TIME=WEEK;
      AREA=DISTSUB;
      END;
	ELSE IF DUMMY=6 THEN DO;
      TIME=PERIOD;
      AREA=SEINEAREA;
      END;
   DROP _TYPE_ _FREQ_ WEEK DUMMY DISTRICT PERIOD QUADRANT DISTSUB SEINEAREA;
RUN;

PROC SORT DATA=MERGEM8;
   BY YEAR GEAR HARVEST TIME AREA;
RUN;

/* CALCULATION OF THE CONTRIBUTION AND VARIANCE */
/* OF EACH TAG CODE IN EACH STRATUM             */

DATA CONTCALC;
   SET MERGEM8;
   IF ((BROOD NE .) AND (N NE .));
   IF (GEAR='SEINE') AND (OLDN > N) THEN DO;
      N2=OLDN><N2;
      A1=N2><A1;
      A2=A1><A2;
      M1=A2><M1;
      M2=M1><M2;
      IF M2=0 OR A2=0 OR N2=0 THEN EXPAND1=0;
      ELSE EXPAND1=(M1*A1*OLDN)/(M2*A2*N2);
      IF OLDN=1 THEN EXPAND2=1;
      ELSE DO;
         IF A1=1 THEN EXPAND2=(N2-1)/(OLDN-1);
         ELSE DO;
            IF M1=1 THEN EXPAND2=((A2-1)*(N2-1))/((A1-1)*(OLDN-1));
            ELSE EXPAND2=((M2-1)*(A2-1)*(N2-1))/((M1-1)*(A1-1)*(OLDN-1));
            END;
         END;
      END;
   ELSE DO;
      N2=N><N2;
      A1=N2><A1;
      A2=A1><A2;
      M1=A2><M1;
      M2=M1><M2;
      IF M2=0 OR A2=0 OR N2=0 THEN EXPAND1=0;
      ELSE EXPAND1=(M1*A1*N)/(M2*A2*N2);
      IF N=1 THEN EXPAND2=1;
      ELSE DO;
         IF A1=1 THEN EXPAND2=(N2-1)/(N-1);
         ELSE DO;
            IF M1=1 THEN EXPAND2=((A2-1)*(N2-1))/((A1-1)*(N-1));
            ELSE EXPAND2=((M2-1)*(A2-1)*(N2-1))/((M1-1)*(A1-1)*(N-1));
            END;
         END;
      END;
   /* rlp note - special MSF CONTRIB logic added HERE */
	IF (HARVEST="MARK SELECT FISHY") THEN DO;
	   N1HAT=EXPAND1*(1)*COUNT;
   	   VARN1=EXPAND1*EXPAND2*N1HAT*(N1HAT-1)+EXPAND1*(1)*N1HAT
         - (N1HAT)**2;
		END;
	/*basically i've set tag ratio = 1 for AK MSF recoveries*/
	/*future refinement should expand using the CLIPPED TAG RATIO, which will require modifying RELEASE data*/
	ELSE DO;
		N1HAT=EXPAND1*(RELEASED/TAGGED)*COUNT;
		VARN1=EXPAND1*EXPAND2*N1HAT*(N1HAT-1)+EXPAND1*(RELEASED/TAGGED)*N1HAT
		- (N1HAT)**2;
		END;
RUN;

/* CALCULATION OF THE CONTRIBUTION BY STRATUM */

PROC SORT DATA=CONTCALC;
   BY YEAR GEAR HARVEST TIME AREA;
RUN;

PROC MEANS DATA=CONTCALC NOPRINT;
   VAR N1HAT VARN1;
   BY YEAR GEAR HARVEST TIME AREA;
   ID N EXPAND1 EXPAND2;
   OUTPUT OUT=STRATOUT SUM=SN1HAT AVARN1;
RUN;

/* CALCULATION OF THE COVARIANCES FOR THE TAG CODES  */
/* IN EACH STRATUM AND THE VARIANCE FOR EACH STRATUM */

DATA COUNTER;
   OYEAR=1;
   OGEAR='UNKNOWN';
   OHARVEST='UNKNOWN';
   OTIME=1;
   OAREA='UNKNOWN';
   STRATUM=1;
   DO OBSNUM=1 TO LAST;
   SET CONTCALC POINT=OBSNUM NOBS=LAST;
   IF (OBSNUM=1) THEN STRATUM+0;
   ELSE IF ((YEAR=OYEAR) AND (GEAR=OGEAR) AND (HARVEST=OHARVEST)
           AND (TIME=OTIME) AND (AREA=OAREA)) THEN STRATUM+0;
   ELSE STRATUM+1;
   OUTPUT;
   OYEAR=YEAR;
   OGEAR=GEAR;
   OHARVEST=HARVEST;
   OTIME=TIME;
   OAREA=AREA;
   RETAIN OYEAR OGEAR OHARVEST OTIME OAREA STRATUM;
   END;
   STOP;
   DROP OYEAR OGEAR OHARVEST OTIME OAREA;
RUN;

PROC FREQ;
   TABLE STRATUM / NOPRINT OUT=FREQOUT;
RUN;

PROC MEANS DATA=FREQOUT NOPRINT;
   VAR COUNT;
   OUTPUT OUT=MAXOUT MAX=MAXSTRAT;
RUN;

DATA _NULL_;
   SET MAXOUT;
   CALL SYMPUT('MAXSTRAT',LEFT(PUT(MAXSTRAT,3.)));
RUN;

PROC TRANSPOSE DATA=COUNTER OUT=TRANS;
   VAR N1HAT;
   BY YEAR GEAR HARVEST TIME AREA;
RUN;

DATA CROSS;
   SET TRANS;
   MAXSTRAT=&MAXSTRAT;
   ARRAY C{*} COL1-COL&MAXSTRAT;
   N1CROSS=0;
   DO I=1 TO MAXSTRAT-1;
      DO J=I+1 TO MAXSTRAT;
         N1CROSS+C{I}*C{J};
      END;
   END;
   KEEP YEAR GEAR HARVEST TIME AREA N1CROSS;
RUN;

DATA VARCOV;
   MERGE STRATOUT CROSS;
   BY YEAR GEAR HARVEST TIME AREA;
   COVSUM=N1CROSS*(EXPAND1*EXPAND2-1);
   SVARN1=AVARN1+2*COVSUM;
   KEEP YEAR GEAR HARVEST TIME AREA N SN1HAT SVARN1;
RUN;

PROC MEANS DATA=MERGEM8 NOPRINT;
   VAR N TAKUN STIKN YAKN;
   BY YEAR GEAR HARVEST TIME AREA;
   OUTPUT OUT=TOTCAT MEAN=N TAKUN STIKN YAKN;

DATA MERGECAT;
   MERGE TOTCAT VARCOV;
   BY YEAR GEAR HARVEST TIME AREA;
   IF N NE .;
   DROP _TYPE_ _FREQ_;
RUN;

DATA MERGECAT1;
   SET MERGECAT;
   WHERE AREA IN ("108","111") AND HARVEST IN ("STIKINE EXCLUSION","TAKU EXCLUSION");
RUN;

/* LOCATING THE TROLL OPENINGS BY PERIOD */

DATA FINDPER;
   SET MERGECAT;
   IF (YEAR=&CURRYEAR) AND (TIME NE 1) AND (GEAR='TROLL')
      AND (HARVEST='TRADITIONAL');
RUN;

PROC FREQ DATA=FINDPER NOPRINT;
   TABLES TIME / OUT=TIMEOUT;
RUN;

DATA _NULL_;
   SET TIMEOUT NOBS=EOF;
   CALL SYMPUT('TOTOPEN',EOF);
RUN;

DATA LABELPER;
   DO I=1 TO &TOTOPEN;
      SET TIMEOUT POINT=I;
      OPENNUM=I;
      YEAR=&CURRYEAR;
      GEAR='TROLL';
      HARVEST='TRADITIONAL';
      OUTPUT;
   END;
   STOP;
RUN;

/* REMOVAL OF BASE AND CWT RECOVERIES FROM TERMINAL EXCLUSION AREAS */

DATA READNONA;
   LENGTH DISTRICT $ 3 SUBDIST $ 2 DISTSUB $ 6 HARVEST $ 17 CODE $ 10;
   INFORMAT DATE YYMMDD10.;
   INFILE "&PATH.&NACWTREC..REC" FIRSTOBS=2 DSD DELIMITER=',' MISSOVER PAD;
   INPUT YEAR HEADNUM $ SAMPLE $ CODE $ SPECCODE HATCHERY $ HARNUM $ GEARNUM $
         DATE WEEK PERIOD DISTRICT $ SUBDIST $ QUADRANT $ LENCODE LENGTH
         EXPANSION TAGRATIO CONTRIB1;
   GEAR=PUT(GEARNUM,$GEARFMT.);
   HARVEST=PUT(HARNUM,$HARVFMT.);
   IF YEAR=&CURRYEAR;
   IF EXPANSION < 1 THEN EXPANSION=1;
   CONTRIB2=EXPANSION*TAGRATIO;
   NDIST=DISTRICT*1;
   IF (GEAR='GILLNET' AND (116 < NDIST < 200)) THEN GEAR='SETNET';
   MONTHNUM=MONTH(DATE);
   DAY=DAY(DATE);
   DISTSUB=TRIM(DISTRICT)||'-'||SUBDIST;
   IF (YEAR=1997 AND GEAR="TROLL" AND MONTHNUM=8 AND DAY=30) THEN DO;
      DAY=31;
      WEEK=WEEK+1;
      END;
   IF (GEAR="SETNET" AND ((1996 LE YEAR LE 2002) OR
      (YEAR GT 2002 AND DISTSUB="182-70"))) OR
      (GEAR = "GILLNET" and DISTRICT="108" AND SUBDIST NE "45" AND &STIKFLAG=1)  OR (GEAR = "GILLNET" and DISTRICT="111" AND &TAKUFLAG=1)  OR
      (GEAR = "TROLL" and HARVEST = "EXPERIM. AREA" and DISTRICT="108" AND SUBDIST NE "45" AND &STIKFLAG=1) OR
      (GEAR = "TROLL" and HARVEST = "EXPERIM. AREA" and DISTRICT="111" AND &STIKFLAG=1);
   IF GEAR="SETNET" THEN DO;
      HARVEST="YAKUTAT EXCLUSION";
      AREA=DISTRICT;
      END;
   ELSE IF DISTRICT="108" THEN DO;
      HARVEST="STIKINE EXCLUSION";
      AREA=DISTRICT;
      END;
   ELSE IF DISTRICT="111" THEN DO;
      HARVEST="TAKU EXCLUSION";
      AREA=DISTRICT;
      END;
   RENAME WEEK=TIME;
   DROP SAMPLE DISTRICT SUBDIST LENGTH LENCODE GEARNUM HARNUM;
RUN;

PROC SORT DATA=READNONA;
   BY YEAR GEAR HARVEST TIME AREA;
RUN;

PROC MEANS DATA=READNONA NOPRINT;
   VAR CONTRIB2;
   BY YEAR GEAR HARVEST TIME AREA;
   OUTPUT OUT=NONAOUT SUM=NONASUM;
RUN;

DATA TRIMTEX;
   SET MERGECAT;
   IF HARVEST IN ("STIKINE EXCLUSION","TAKU EXCLUSION","YAKUTAT EXCLUSION")
      OR (HARVEST="TERMINAL AREA" AND AREA="112-22" AND YEAR GE 1996);
RUN;

DATA MERGTEX;
   MERGE NONAOUT TRIMTEX;
   BY YEAR GEAR HARVEST TIME AREA;
   IF HARVEST="TERMINAL AREA" THEN DO;
      NONASUM=0;
      SN1HAT=0;
      END;
   HATCHERY=SUM(NONASUM,SN1HAT);
   NEGHATCH=(0-HATCHERY);
   WILD=SUM(N,NEGHATCH);
   PWILD=MAX(0,WILD);
RUN;

PROC SORT DATA=MERGTEX;
   BY YEAR HARVEST GEAR;
RUN;

PROC MEANS DATA=MERGTEX NOPRINT;
   VAR PWILD;
   WHERE HARVEST NE "TERMINAL AREA";
   BY YEAR HARVEST GEAR;
   OUTPUT OUT=TEXOUT SUM=PWILDSUM;
RUN;

PROC MEANS DATA=MERGTEX NOPRINT;
   VAR PWILD;
   WHERE HARVEST = "TERMINAL AREA";
   BY YEAR HARVEST;
   OUTPUT OUT=TERMTEXOUT SUM=PWILDSUM;
RUN;

DATA TERMMERGTEX;
   MERGE TERMTEXOUT MERGTEX;
   WHERE HARVEST = "TERMINAL AREA";
   BY YEAR HARVEST;
RUN;

DATA MERGTEX2;
   MERGE MERGTEX TEXOUT TERMMERGTEX ;
   BY YEAR HARVEST GEAR;
RUN;

DATA BASETEX;
   SET MERGTEX2;
   IF PWILDSUM > 0 THEN DO;
      BASEFRAC=(PWILD/PWILDSUM);
      IF HARVEST="TERMINAL AREA" THEN TEXBASE=500*BASEFRAC;
      ELSE IF HARVEST="STIKINE EXCLUSION" AND GEAR = 'GILLNET' THEN TEXBASE=428*BASEFRAC;
      ELSE IF HARVEST="TAKU EXCLUSION" AND GEAR = 'GILLNET' THEN TEXBASE=934*BASEFRAC;
      ELSE IF HARVEST="STIKINE EXCLUSION" AND GEAR = 'TROLL' THEN TEXBASE=157*BASEFRAC;
      ELSE IF HARVEST="TAKU EXCLUSION" AND GEAR = 'TROLL' THEN TEXBASE=0*BASEFRAC;
      ELSE IF HARVEST="YAKUTAT EXCLUSION" THEN DO;
         IF (1996 LE YEAR LE 2002) THEN YAKBASE=2000;
         ELSE IF YEAR GT 2002 THEN YAKBASE=776;
         TEXBASE=YAKBASE*BASEFRAC;
         END;
      END;
   ELSE TEXBASE=0;
   WILD1HAT=PWILD-TEXBASE;
RUN;

PROC SORT DATA=TRIMTEX;
   BY YEAR GEAR HARVEST TIME AREA;
RUN;

PROC SORT DATA=BASETEX;
   BY YEAR GEAR HARVEST TIME AREA;
RUN;

DATA MERGTEX3;
   MERGE TRIMTEX BASETEX;
   BY YEAR GEAR HARVEST TIME AREA;
   THARVEST=HARVEST;
   TTEXBASE=TEXBASE;
   IF GEAR = "TROLL" THEN HARVEST = "EXPERIM. AREA";
   ELSE HARVEST="TRADITIONAL";
   TEXBASE=0;
   IF WILD < 0 THEN N=SUM(HATCHERY,WILD);
   ELSE N=HATCHERY;
   IF (AREA NE "112-22" AND N > 0) THEN OUTPUT;
   HARVEST=THARVEST;
   TEXBASE=TTEXBASE;
   N=PWILD;
   IF WILD1HAT > 0 THEN SN1HAT=WILD1HAT;
   ELSE SN1HAT=0;
   SVARN1=0;
   OUTPUT;
RUN;

PROC SORT DATA=MERGTEX3;
   BY YEAR GEAR HARVEST TIME AREA;
RUN;

DATA TRIMCAT;
   SET MERGECAT;
   IF NOT(HARVEST IN ("STIKINE EXCLUSION","TAKU EXCLUSION","YAKUTAT EXCLUSION")
      OR (HARVEST="TERMINAL AREA" AND AREA="112-22" AND YEAR GE 1996));
   IF (TAKUN NE .) THEN N=N-TAKUN;
   ELSE IF (STIKN NE .) THEN N=N-STIKN;
   ELSE IF (YAKN NE .) THEN N=N-YAKN;
RUN;

DATA MERGCAT2;
   MERGE TRIMCAT LABELPER;
   BY YEAR GEAR HARVEST TIME;
RUN;

DATA COMBCAT;
   LENGTH HARTYPE $ 17;
   MERGE MERGCAT2 MERGTEX3;
   BY YEAR GEAR HARVEST TIME AREA;
   IF (GEAR="TROLL" AND HARVEST="TRADITIONAL") THEN DO;
      IF (YEAR=&PREVYEAR AND TIME=&MAXTRAD1) THEN HARTYPE="WINTER-1";
      ELSE IF (YEAR=&CURRYEAR AND TIME=1) THEN HARTYPE="WINTER-2";
      ELSE HARTYPE="SUMMER"||"-"||TRIM(LEFT(OPENNUM));
      END;
   ELSE IF (HARVEST="TERMINAL AREA") AND (AREA IN ("101-46","101-48","101-95",
      "106-44","107-35","107-45","108-45")) THEN DO;
      HARTYPE="TERMINAL REAL";
      SN1HAT=N;
      SVARN1=0;
      END;
   ELSE IF (HARVEST="TERMINAL AREA" AND AREA="113-38" AND YEAR GE 2013) THEN DO;
      HARTYPE="TERMINAL REAL";
      SN1HAT=N;
      SVARN1=0;
      END;
   ELSE IF (HARVEST="TERMINAL AREA" AND AREA="112-22" AND YEAR GE 1996)
      THEN HARTYPE="TERMINAL REAL";
   ELSE HARTYPE=HARVEST;
RUN;


PROC SORT DATA=COMBCAT;
   BY GEAR YEAR TIME AREA;
RUN;

/* OUTPUTTING THE CONTRIBUTIONS BY STRATA */

TITLE2 'CONTRIBUTIONS BY STRATA';

PROC PRINT DATA=COMBCAT NOOBS SPLIT='*' UNIFORM;
   FORMAT N SN1HAT SVARN1 TEXBASE COMMA9.;
   LABEL HARTYPE='HARVEST*TYPE' N='CATCH' SN1HAT='CONTRIBUTION'
         SVARN1='VARIANCE' TEXBASE='TERMINAL*EXCLUSION*BASE';
   WHERE (TIME NE .) AND ((N GT 0) OR (SN1HAT GT 0));
   VAR YEAR HARTYPE TIME AREA N SN1HAT SVARN1 TEXBASE;
   BY GEAR;
   PAGEBY GEAR;
RUN;

/* DATA STEP WHICH READS THE SPORT CONTRIBUTION AND VARIANCE */

DATA SPORTIN;
   LENGTH HARTYPE $ 17 COMMENTS $ 30;
   INFILE "&PATH.&SPORT..SPT" FIRSTOBS=2 DSD MISSOVER PAD;
   INPUT YEAR HN HN1HAT HVARN1 HARNUM $ COMMENTS $;
   IF YEAR=&CURRYEAR;
   HARTYPE=PUT(HARNUM,$HARVFMT.);
   IF HARTYPE="TERMINAL AREA" THEN HARTYPE="TERMINAL REAL";
   ELSE IF HARTYPE IN ("STIKINE EXCLUSION","TAKU EXCLUSION",
      "YAKUTAT EXCLUSION") THEN DO;
      IF HARTYPE="STIKINE EXCLUSION" THEN TEXBASE2=2815;
      ELSE IF HARTYPE="TAKU EXCLUSION" THEN TEXBASE2=2566;
      ELSE IF HARTYPE="YAKUTAT EXCLUSION" THEN DO;
         IF (1996 LE YEAR LE 2002) THEN TEXBASE2=200;
         ELSE IF YEAR GT 2002 THEN TEXBASE2=210;
         END;
      IF TEXBASE2 < HN1HAT THEN HN1HAT=HN1HAT-TEXBASE2;
      ELSE HN1HAT=0;
      END;
   GEAR='SPORT';
RUN;

PROC SORT DATA=SPORTIN;
   BY YEAR GEAR HARTYPE;
RUN;

/* CALCULATION OF THE CONTRIBUTION BY GEAR AND HARVEST TYPE */

PROC SORT DATA=COMBCAT;
   BY YEAR GEAR HARTYPE;
RUN;

PROC MEANS DATA=COMBCAT NOPRINT;
   VAR N SN1HAT SVARN1 TEXBASE;
   BY YEAR GEAR HARTYPE;
   OUTPUT OUT=HAROUT SUM=HN HN1HAT HVARN1 TEXBASE2;
RUN;

DATA HARCOMB;
   MERGE HAROUT SPORTIN;
   BY YEAR GEAR HARTYPE;
RUN;

PROC SORT DATA=HARCOMB;
   BY GEAR HARTYPE YEAR;
RUN;

TITLE2 'CONTRIBUTION BY GEAR AND HARVEST TYPE';

PROC PRINT DATA=HARCOMB NOOBS SPLIT='*' UNIFORM;
   FORMAT HN HN1HAT HVARN1 TEXBASE2 COMMA9.;
   VAR YEAR GEAR HARTYPE HN HN1HAT HVARN1 TEXBASE2;
   WHERE (HN GT 0) OR (HN1HAT GT 0);
   LABEL HARTYPE='HARVEST*TYPE' HN='CATCH' HN1HAT='CONTRIBUTION'
         HVARN1='VARIANCE' TEXBASE2='TERMINAL*EXCLUSION*BASE';
RUN;

/* CALCULATION OF THE CONTRIBUTION BY GEAR TYPE */

PROC MEANS DATA=HARCOMB NOPRINT;
   VAR HN HN1HAT HVARN1 TEXBASE2;
   BY GEAR;
   OUTPUT OUT=GEAROUT SUM=GN GN1HAT GVARN1 TEXBASE3;
RUN;

DATA ADDYEAR;
   SET GEAROUT;
   YEAR=&CURRYEAR;
RUN;

TITLE2 'CONTRIBUTION BY GEAR';

PROC PRINT DATA=ADDYEAR NOOBS SPLIT='*' UNIFORM;
   FORMAT GN GN1HAT GVARN1 TEXBASE3 COMMA9.;
   VAR YEAR GEAR GN GN1HAT GVARN1 TEXBASE3;
   LABEL GN='CATCH' GN1HAT='CONTRIBUTION' GVARN1='VARIANCE'
         TEXBASE3='TERMINAL*EXCLUSION*BASE';
RUN;

/* CALCULATION OF THE TOTAL CONTRIBUTION AND THE ADDON */

PROC MEANS DATA=ADDYEAR NOPRINT;
   VAR GN GN1HAT GVARN1 TEXBASE3;
   BY YEAR;
   OUTPUT OUT=TOTOUT SUM=TN TN1HAT TVARN1 TEXBASE4;
RUN;

DATA COMADDON;
   SET TOTOUT;
   RENAME TN=CATCH TN1HAT=CONT TVARN1=VAR;
   IF YEAR < 1996 THEN INVNORM=1.644853;
   ELSE INVNORM=1.281551;
   RISK=(INVNORM)*SQRT(TVARN1);
   BASE=5000;
   ADDON=TN1HAT-RISK-BASE;
   TREATY=TN-ADDON;
   DROP _TYPE_ _FREQ_;
RUN;

TITLE 'THE TOTAL ALASKA CHINOOK HATCHERY ADDON';
TITLE2 ' ';

PROC PRINT DATA=COMADDON NOOBS SPLIT='*' UNIFORM;
   FORMAT CATCH TREATY ADDON CONT RISK BASE TEXBASE4 COMMA9. VAR COMMA11.;
   LABEL CATCH='TOTAL*CATCH' TREATY='TREATY*CATCH' CONT='TOTAL*CONTRIBUTION'
         VAR='TOTAL*VARIANCE' RISK='RISK*ADJUSTMENT'
         TEXBASE4='TERMINAL*EXCLUSION*BASE';
   VAR YEAR CATCH TREATY ADDON CONT VAR RISK BASE TEXBASE4;
RUN;

DATA ADDCAT;
   LENGTH CATEGORY $ 18;
   SET COMADDON;
   RENAME CONT=CONTRIB
          VAR=VARIANCE
          TEXBASE4=TEXBASE;
   CATEGORY="ADDON TOTAL";
RUN;

PROC SORT DATA=ADDCAT;
   BY YEAR CATEGORY;
RUN;

DATA ADDTABLE;
   LENGTH CATEGORY $ 18 SUBCAT $ 17;
   SET HARCOMB;
   RENAME HN=CATCH
          HVARN1=VARIANCE;
   IF HARTYPE="M-I-C" THEN DO;
      CATEGORY="ANNETTE ISLAND";
      CONTRIB=HN1HAT;
      TERMINAL=0;
      END;
   ELSE IF HARTYPE IN ("YAKUTAT EXCLUSION","TAKU EXCLUSION",
      "STIKINE EXCLUSION") THEN DO;
      CATEGORY="TERMINAL EXCLUSION";
      SUBCAT=HARTYPE;
      CONTRIB=0;
      TERMINAL=HN1HAT;
      END;
   ELSE IF GEAR IN ("SEINE","GILLNET","SETNET") THEN DO;
      CATEGORY="NET";
      IF HARTYPE="TERMINAL REAL" THEN DO;
         CONTRIB=0;
         TERMINAL=HN1HAT;
         END;
      ELSE DO;
         CONTRIB=HN1HAT;
         TERMINAL=0;
         END;
      END;
   ELSE IF GEAR="TROLL" THEN DO;
      IF HARTYPE="TERMINAL AREA" THEN HARTYPE="EXPERIM. AREA";
      SUBCAT=HARTYPE;
      IF HARTYPE="TERMINAL REAL" THEN DO;
         CATEGORY="TROLL-JUNE";
         CONTRIB=0;
         TERMINAL=HN1HAT;
         END;
      ELSE DO;
         CONTRIB=HN1HAT;
         TERMINAL=0;
         IF SUBSTR(HARTYPE,1,6)="WINTER" THEN DO;
            CATEGORY="TROLL-WINTER";
            END;
         ELSE IF HARTYPE IN ("EXPERIM. AREA","HATCHERY ACCESS",
                             "TERMINAL AREA") THEN DO;
            CATEGORY="TROLL-JUNE";
            END;
         ELSE IF HARTYPE IN ("CONFISCATED") THEN DO;
            CATEGORY="CONFISCATED";
            END;
		ELSE IF HARTYPE IN ("MARK SELECT FISHY") THEN DO;
            CATEGORY="TROLL-SUMMER";
			SUBCAT="SUMMER-MSF";
            END;
         ELSE IF SUBSTR(HARTYPE,1,6)="SUMMER" THEN DO;
            CATEGORY="TROLL-SUMMER";
            END;
         END;
      END;
   ELSE IF GEAR="SPORT" THEN DO;
      CATEGORY=GEAR;
      IF HARTYPE="TERMINAL REAL" THEN DO;
         CONTRIB=0;
         TERMINAL=HN1HAT;
         END;
      ELSE DO;
         CONTRIB=HN1HAT;
         TERMINAL=0;
         END;
      END;
RUN;

PROC SORT DATA=ADDTABLE;
   BY CATEGORY SUBCAT GEAR;
RUN;

PROC MEANS DATA=ADDTABLE NOPRINT;
   VAR CATCH CONTRIB TERMINAL VARIANCE TEXBASE2;
   BY CATEGORY SUBCAT GEAR;
   OUTPUT OUT=TABLESUM SUM=CATCH CONTRIB TERMINAL VARIANCE TEXBASE;
RUN;

DATA ADDYEAR2;
   SET TABLESUM;
   YEAR=&CURRYEAR;
RUN;

DATA MERGEADD;
   MERGE ADDCAT ADDYEAR2;
   BY YEAR CATEGORY;
   IF CATEGORY NE "";
RUN;

DATA TEMPLATE;
   YEAR=&CURRYEAR;
   INFILE CARDS DSD MISSOVER PAD;
   LENGTH CATEGORY $ 18 SUBCAT $ 17;
   INPUT CATEGORY $ SUBCAT $ GEAR $;
   CARDS;
   ABLANK            ,BLANK            ,BLANK
   ANNETTE ISLAND    ,                 ,GILLNET
   ANNETTE ISLAND    ,                 ,SEINE
   ANNETTE ISLAND    ,                 ,TRAP
   ANNETTE ISLAND    ,                 ,TROLL
   CONFISCATED       ,CONFISCATED      ,TROLL
   NET               ,                 ,GILLNET
   NET               ,                 ,SEINE
   NET               ,                 ,SETNET
   SPORT             ,                 ,SPORT
   TERMINAL EXCLUSION,STIKINE EXCLUSION,GILLNET
   TERMINAL EXCLUSION,STIKINE EXCLUSION,SPORT
   TERMINAL EXCLUSION,STIKINE EXCLUSION,TROLL
   TERMINAL EXCLUSION,TAKU EXCLUSION   ,GILLNET
   TERMINAL EXCLUSION,TAKU EXCLUSION   ,SPORT
   TERMINAL EXCLUSION,TAKU EXCLUSION   ,TROLL
   TERMINAL EXCLUSION,YAKUTAT EXCLUSION,SETNET
   TERMINAL EXCLUSION,YAKUTAT EXCLUSION,SPORT
   TROLL-JUNE        ,EXPERIM. AREA    ,TROLL
   TROLL-JUNE        ,HATCHERY ACCESS  ,TROLL
   TROLL-JUNE        ,TERMINAL REAL    ,TROLL
   TROLL-SUMMER      ,SUMMER-1         ,TROLL
   TROLL-SUMMER      ,SUMMER-2         ,TROLL
   TROLL-SUMMER      ,SUMMER-3         ,TROLL
   TROLL-SUMMER      ,SUMMER-4         ,TROLL
   TROLL-SUMMER      ,SUMMER-MSF       ,TROLL
   TROLL-WINTER      ,WINTER-1         ,TROLL
   TROLL-WINTER      ,WINTER-2         ,TROLL
   ;
RUN;

DATA MERGTPLT;
   MERGE MERGEADD TEMPLATE;
   BY CATEGORY SUBCAT GEAR YEAR;
   IF SUBCAT="STIKINE EXCLUSION" AND GEAR = 'GILLNET' THEN TEXBASE=428;
   ELSE IF SUBCAT="TAKU EXCLUSION" AND GEAR = 'GILLNET' THEN TEXBASE=934;
   ELSE IF SUBCAT="STIKINE EXCLUSION" AND GEAR = 'TROLL' THEN TEXBASE=157;
   ELSE IF SUBCAT="TAKU EXCLUSION" AND GEAR = 'TROLL' THEN TEXBASE=0;
RUN;

DATA _NULL_;
   SET MERGTPLT NOBS=LASTLINE;
   CALL SYMPUT('LAST',LASTLINE);
RUN;

OPTIONS PS=&LAST;

DATA _NULL_;
   FILE "&PATH.ADD&CURRYEAR..OUT" N=&LAST;
   FORMAT CONTRIB 9.2 VARIANCE 11.2 ADDON 9.2 RISK 8.2 TREATY 9.2 TEXBASE 7.0;
   COMMA=',';
   DO LINE=1 TO &LAST;
      SET MERGTPLT;
      IF LINE=1 THEN DO;
         PUT #LINE "CATEGORY" COMMA "SUBCAT" COMMA "GEAR" COMMA "CATCH" COMMA
                "CONTRIB" COMMA "TERMINAL" COMMA "VARIANCE" COMMA "ADDON" COMMA
                "RISK" COMMA "BASE" COMMA "TREATY" COMMA "TEXBASE" COMMA "YEAR";
         END;
      ELSE DO;
         PUT #LINE CATEGORY COMMA SUBCAT COMMA GEAR COMMA CATCH COMMA
                CONTRIB COMMA TERMINAL COMMA VARIANCE COMMA ADDON COMMA
                RISK COMMA BASE COMMA TREATY COMMA TEXBASE COMMA YEAR;
         END;
   END;
   PUT _PAGE_;
RUN;

PROC SORT DATA=CONTCALC;
   BY YEAR GEAR HARVEST TIME AREA HATCHERY;
RUN;

PROC MEANS DATA=CONTCALC NOPRINT;
   VAR N1HAT;
   BY YEAR GEAR HARVEST TIME AREA HATCHERY;
   WHERE AREA IN ("108","111") and HATCHERY = "CRYSTAL" AND HARVEST IN ("STIKINE EXCLUSION","TAKU EXCLUSION");
   OUTPUT OUT=CLHOUT SUM=CLH_CWT;
RUN;

PROC SORT DATA = TRIMWEEK;
   BY YEAR DISTRICT GEAR HARVEST WEEK;
RUN;

PROC MEANS DATA = TRIMWEEK NOPRINT;
   VAR N;
   BY YEAR DISTRICT GEAR HARVEST WEEK;
   WHERE YEAR = &CURRYEAR and DISTRICT IN ('108','111') AND (16 < WEEK < 30) AND GEAR IN ('TROLL','GILLNET');
   OUTPUT OUT=TBRCATCH SUM=CATCH;
RUN;

DATA _NULL_;
   SET TBRCATCH NOBS=LASTLINE;
   CALL SYMPUT('LAST2',LASTLINE + 1);
RUN;

DATA _NULL_;
   FILE "&PATH.TBRCATCH&CURRYEAR..OUT" N = &LAST2;
   COMMA=',';
   PUT #1 "GEAR" COMMA "YEAR" COMMA "HARVEST" COMMA "TIME" COMMA "AREA" COMMA "TOTAL CATCH";
   DO LINE=1 TO &LAST2;
      SET TBRCATCH;
      LINE2 = LINE + 1;
      IF GEAR='TROLL' AND HARVEST='TRADITIONAL' THEN DO;
         IF WEEK LT 26 THEN HARVEST='TRADITIONAL WIN2';
         ELSE HARVEST='TRADITIONAL SUMM1';
         END;
      PUT #LINE2 GEAR COMMA YEAR COMMA HARVEST COMMA WEEK COMMA DISTRICT COMMA CATCH;
   END;
RUN;

ods html close; /* close previous results */
=======
>>>>>>> ae334e98edfbfbb86be8e5a03973249aeef80c5c
